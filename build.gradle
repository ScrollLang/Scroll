import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'com.github.johnrengelman.shadow' version '8.1.1'
	id 'fabric-loom' version '1.4-SNAPSHOT'
	id 'maven-publish'
	id 'eclipse'
	id 'java'
}

compileTestJava.options.encoding = 'UTF-8'
compileJava.options.encoding = 'UTF-8'

base {
	archivesName = project.archives_base_name
}

repositories {
	mavenCentral()
    maven {
        url = 'https://maven.pkg.github.com/ScrollLang/skript-parser'
        credentials {
            username = System.getenv("PACKAGES_USER") ?: project.findProperty("scrollUsername")
            password = System.getenv("PACKAGES_KEY") ?: project.findProperty("scrollPassword")
        }
    }
}

loom {
	splitEnvironmentSourceSets()

	mods {
		scroll {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

dependencies {

	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Adventure API
	modImplementation include("net.kyori:adventure-platform-fabric:5.9.0")

	// Reflections
	modImplementation include('org.reflections:reflections:0.10.2')
	modImplementation include('org.javassist:javassist:3.30.2-GA')

	// TOML
	modImplementation include('org.tomlj:tomlj:1.1.1')
	modImplementation include('org.antlr:antlr4-runtime:4.13.1')

	// skript-parser
	modImplementation include('org.scrolllang:skript-parser:alpha2')
	//implementation include(project(':skript-parser'))

}

publishing {
    repositories {
        maven {
            name = 'Scroll'
            url = 'https://maven.pkg.github.com/ScrollLang/Scroll'
            credentials {
                username = System.getenv("PACKAGES_USER") ?: project.findProperty("scrollUsername")
                password = System.getenv("PACKAGES_KEY") ?: project.findProperty("scrollPassword")
            }
        }
        publications {
            gpr(MavenPublication) {
                from(components.java)
                version = project.version
                groupId = 'org.scrolllang'
                def releases = 'scroll' // Don't modify
                def snapshots = 'scroll-dev' // Don't modify
                artifactId = version.contains('dev') ? snapshots : releases
            }
        }
    }
}

processResources {
	inputs.property 'version', project.version
	filesMatching('fabric.mod.json') {
		expand 'version': project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from('LICENSE') {
		rename {
			"${it}_${project.base.archivesName.get()}"
		}
	}
}

processResources {
	filter ReplaceTokens, tokens: ["version": project.property("version")]
}
